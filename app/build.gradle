apply plugin: 'com.android.application'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionCode = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'origin/master', '--first-parent', '--count'
        standardOutput = stdout
    }
    return Integer.valueOf(stdout.toString().trim());
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "de.familiep.mobileinformationgain"
        minSdkVersion 21
        targetSdkVersion 25
        versionName getVersionName()
        versionCode getVersionCode()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            buildConfigField "int", "INQUIRY_DUR", "0"
        }
        pilot {
            minifyEnabled false
            debuggable false
            buildConfigField "int", "INQUIRY_DUR", "1"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        study {
            minifyEnabled false
            debuggable false
            buildConfigField "int", "INQUIRY_DUR", "7"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    testCompile "pl.pragmatists:JUnitParams:1.0.5" // JUnitParams
    testCompile 'junit:junit:4.12'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
